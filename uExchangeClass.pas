unit uExchangeClass;

interface

uses
  System.SysUtils, System.Classes,
  System.Generics.Collections;

type
  TExchange = (BiBox, Binance, Bitfinex, Bitstamp, Bittrex, HitBTC, Huobi, Kraken, Okex, Poloniex);

  TExchangeHelper = record helper for TExchange
    private
      const
        cArrExchangesText: array [TExchange] of string = ('BiBox', 'Binance', 'Bitfinex',
                                                           'Bitstamp', 'Bittrex', 'HitBTC',
                                                           'Huobi', 'Kraken', 'Okex', 'Poloniex');
    public
      function ToString: string; overload; inline;
      class function ExchangeFromString(const aExchangeName: string): TExchange; inline; static;
      class function Count: Integer; inline; static;
  end;

  TTrade = record
    private
      FAmount: Double;
      FIsBuyerMaker: Boolean;
    public
      constructor Create(const aAmount: Double; const aIsBuyerMaker: Boolean);

      property Amount: Double read FAmount;
      property IsBuyerMaker: Boolean read FIsBuyerMaker;
  end;

  TPairDepth = class
    private
      FPrice: Currency;
      FAmount: Double;
      FExchangeList: TStringList;
    function GetExchangeName: String;
    public
      constructor Create(const aPrice: Currency;
                         const aAmount: Double;
                         const aExchange: TExchange); overload;

      constructor Create(const aPrice: Currency;
                         const aAmount: Double;
                         const aExchangeList: string); overload;

      destructor  Destroy; override;

      procedure AddPair(const aPairDepth: TPairDepth);

      property Price: Currency read FPrice;
      property Amount: Double read FAmount;
      property ExchangeList: String read GetExchangeName;
  end;

  TStatistics24h = class
    private
      FSymbol: String;
      FPriceChange: Double; // ���� ���� �� 24 ������
      FPriceChangePercent: String; // ���� ���� �� 24 ������ � ��������
      FWeightedAvgPrice: Currency; // ������� ����
      FPrevClosePrice: Currency; // ��������� ���� ��������
      FLastPrice: Currency; // ������� ����
      FLastQty: Double; // �������� ���
      FBidPrice: Currency; // ֳ�� �������
      FAskPrice: Currency; // ֳ�� �������
      FOpenPrice: Currency; // ֳ�� ��������
      FHighPrice: Currency; // ���� ������ ����
      FLowPrice: Currency; // ���� ������ ����
      FVolume: Currency; // ��� ����� �� 24
      FCount: Integer; // ʳ������ ������
    public
      property Symbol: String read FSymbol write FSymbol;
      property PriceChange: Double read FPriceChange write FPriceChange;
      property PriceChangePercent: String read FPriceChangePercent write FPriceChangePercent;
      property WeightedAvgPrice: Currency read FWeightedAvgPrice write FWeightedAvgPrice;
      property PrevClosePrice: Currency read FPrevClosePrice write FPrevClosePrice;
      property LastPrice: Currency read FLastPrice write FLastPrice;
      property LastQty: Double read FLastQty write FLastQty;
      property BidPrice: Currency read FBidPrice write FBidPrice;
      property AskPrice: Currency read FAskPrice write FAskPrice;
      property OpenPrice: Currency read FOpenPrice write FOpenPrice;
      property HighPrice: Currency read FHighPrice write FHighPrice;
      property LowPrice: Currency read FLowPrice write FLowPrice;
      property Volume: Currency read FVolume write FVolume;
      property Count: Integer read FCount write FCount;
  end;

  TOnUpdateDepth = procedure (const aBidsList, aAsksList: TList<TPairDepth>; const aTotalBids, aTotalAsks: Double) of Object;
  TOnUpdateStatistics24h = procedure of Object;

implementation

uses
  uSettigns, System.StrUtils,
  System.Math;

{ TPairDepth }

procedure TPairDepth.AddPair(const aPairDepth: TPairDepth);
begin
  if Assigned(aPairDepth) then
  begin
    FAmount := FAmount + aPairDepth.Amount;

    FExchangeList.Add(aPairDepth.ExchangeList);
  end;
end;

constructor TPairDepth.Create(const aPrice: Currency;
                              const aAmount: Double;
                              const aExchange: TExchange);
begin
  FPrice := aPrice;
  FAmount := aAmount;

  FExchangeList := TStringList.Create;

  FExchangeList.Add(aExchange.ToString + ' - ' + Format('%n', [SimpleRoundTo(aAmount)]));
end;

constructor TPairDepth.Create(const aPrice: Currency;
                              const aAmount: Double;
                              const aExchangeList: string);
begin
  FPrice := aPrice;
  FAmount := aAmount;

  FExchangeList := TStringList.Create;
  FExchangeList.Add(aExchangeList);
end;

destructor TPairDepth.Destroy;
begin
  FreeAndNil(FExchangeList);

  inherited;
end;

function TPairDepth.GetExchangeName: String;
begin
  Result := FExchangeList.Text.Trim;
end;

{ TExchange }

class function TExchangeHelper.Count: Integer;
begin
  Result := Length(cArrExchangesText);
end;

class function TExchangeHelper.ExchangeFromString(const aExchangeName: string): TExchange;
var
  LIndex: Integer;
begin
  LIndex := IndexText(aExchangeName, cArrExchangesText);

  if LIndex > -1 then
    Result := TExchange(LIndex)
  else
    raise Exception.Create('The name "' + aExchangeName + '" was not found.');
end;

function TExchangeHelper.ToString: string;
begin
  Result := cArrExchangesText[Self];
end;

{ TTrade }

constructor TTrade.Create(const aAmount: Double; const aIsBuyerMaker: Boolean);
begin
  FAmount := aAmount;
  FIsBuyerMaker := aIsBuyerMaker;
end;

end.
